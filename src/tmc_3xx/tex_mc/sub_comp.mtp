//--------------------------------------------------------------------------//
//		TeX Module Control with HIDEMARU Macro								//
//																			//
//			Sugi-Macro  -Macro Factory for HIDEMARU Editor-					//
//			%COPYRIGHT%		//
//																			//
//--------------------------------------------------------------------------//

//!HEADER
//	INTERNAL VERSION	%VER_MAJOR%.%VER_MINOR%.%VER_RELEASE%
//	PURPOSE				PREPARATION FOR SUBSEQUENCE TYPESETTING
//	PLATFORM			INDEPENDENT
//

//	Registry Data
$REG_ROOT		=	"Sugi_Macro\\TeXModuleControl";
$REG_K_MACROCONFIG	=	"MacroConfig";	//	マクロの設定定義のサブキー

//	Get MacroFolder's Name
call _kernel_getmacrodir;
$MACRODIR = $$return + "\\tex_mc";

//	Macro Config Information
call _kernel_getdatastr $REG_K_MACROCONFIG, "HmOpenOption";
$OPTIONS  = $$return;
call _kernel_getdatanum $REG_K_MACROCONFIG, "Ignore_Preamble";
#PREAMBLE = ##return;

//	Message Data Information
$MSGINI	=	$MACRODIR + "\\message.ini";

$DYNINI	=	$MACRODIR + "\\dyndata.ini";
$DYN_MODULECMD	=	"ModuleCmd";	//	モジュール動作定義のサブキー
$DYN_PROJECT	=	"CurrentProjectInfo";	//	処理するファイルの情報

$PROJECTDIR = getinistr($DYNINI,$DYN_PROJECT,"ProjectDir");
$ORGFILENAME = getinistr($DYNINI,$DYN_PROJECT,"FileName");

$SUB_PARENT = "_tmc_par.tex";
$SUB_CHILD = "_tmc_sub.tex";
$SUB_CHILD_NOEXT = "_tmc_sub";

//	Main Routine
main:
	if( !selecting ){
		call _local_selectregion;
		if( !##return ) call _local_exit 0;
	}
	call _local_makesubfile;
	if( ##return ){
		writeinistr $DYNINI, "SubComp_Data", "OrgFileName", $ORGFILENAME;
		writeininum $DYNINI, "SubComp_Data", "Error_Line_OffSet", #ELOFF-1;
		writeinistr $DYNINI, $DYN_PROJECT, "FileName", $SUB_PARENT;
		call _local_exit 1;
	} else call _local_exit 0;
	endmacro;	//	not reached.

//	End of Main

_local_exit:
	call _kernel_write_execcmd ##1;
	endmacro;

_local_selectregion:
	inputpos "範囲選択開始位置にカーソルをあわせて[Enter](キャンセルは[ESC])";
	if( !result ) return 0;
	beginsel;
	inputpos "範囲選択終了位置にカーソルをあわせて[Enter](キャンセルは[ESC])";
	if( !result ) return 0;
	return 1;

_local_makesubfile:
	copy;
	disabledraw; disableinvert;
	moveto seltopx, seltopy;
	##col = column;
	#ELOFF = lineno;

	##hwnd = hidemaruhandle( 0 );
	title "テンポラリファイルの作成中(" + $SUB_CHILD + ")";
	call _kernel_openhm $PROJECTDIR + "\\" + $SUB_CHILD;
	if( !##return ) return 0;
	else ##s_hwnd = ##return;
	selectall; delete; poppaste;
	gofileend; insert "\n";
	save;
	call _kernel_activatehm ##hwnd;
	call _kernel_closehm ##s_hwnd;
	title 0;

	disableinvert;
	disabledraw;
	$$s_buf = searchbuffer; ##s_opt = searchoption;
	if( #PREAMBLE == 2 ){
		question "プリアンブル(\"\\begin{document}\"以前)を使用しますか？";
		#PREAMBLE = 1 - result;
	}
	if( #PREAMBLE ) setsearch "[^%]*document(style|class)", 16;
	else setsearch "^[^%]*\\\\begin\\{document\\}", 16;
	gofiletop;
	beginsel;
	finddown;
	##latex = result;
	if( ##latex ){
		movetolineno 1, lineno + 1;
		copy;
	} else {
		escape;
		setclipboard "%%%% Generated by TeX Module Control Macro\n";
	}

	title "テンポラリファイルの作成中(" + $SUB_PARENT + ")";
	call _kernel_openhm $PROJECTDIR + "\\" + $SUB_PARENT;
	if( !##return ) return 0;
	else ##s_hwnd = ##return;
	selectall; delete;
	poppaste;
	gofileend;
	if( #PREAMBLE ) insert "\n\\begin{document}\n";
	insert "\n\\include{" + $SUB_CHILD_NOEXT + "}\n";
	if( ##latex ) insert "\\end{document}\n";
	else insert "\\bye\n";
	//	MakeIndex を実行しないための処理
	setsearch "^[^%]*\\\\makeindex", 16;
	findup;
	if( result ){
		golinetop2;
		insert "%";
	}
	save;
	call _kernel_activatehm ##hwnd;
	call _kernel_closehm ##s_hwnd;
	title 0;

	gofileend; movetolineno ##col, #ELOFF;
	setsearch $$s_buf, ##s_opt;
	enableinvert; enabledraw;

	return 1;


//------------------------------------------//
//	- Module Control Service Functions -	//
//------------------------------------------//

