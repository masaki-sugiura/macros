//--------------------------------------------------------------------------//
//		TeX Module Control with HIDEMARU Macro								//
//																			//
//			Sugi-Macro  -Macro Factory for HIDEMARU Editor-					//
//			%COPYRIGHT%		//
//																			//
//--------------------------------------------------------------------------//

//!HEADER
//	INTERNAL VERSION	%VER_MAJOR%.%VER_MINOR%.%VER_RELEASE%
//	PURPOSE				MODULE CONTROL MICRO KERNEL
//	PLATFORM			dvi2tty
//

//--------------------------------------------------------------------------//
//	このマクロは dvi2tty 専用です。											//
//--------------------------------------------------------------------------//

//	Platform identifier
//		呼び出しマクロが正しいかどうかのチェックやレジストリアクセスに使用
//		レジストリキー名の仕様に準拠する必要がある
//
$DVIWARE	=	"DVI2TTY";

//----------------------------------//
//	Common global data				//
//----------------------------------//

//	Definition of return code from module control micro-kernel
//		マクロからの戻り値の定義
//
#RETURN_SUCCESS	=	  0;	//	正常にプレビューできた
#RETURN_FAILED	=	  1;	//	DVIファイルがなかった
#RETURN_ERROR	=	255;	//	異常終了

//	Base registry path
//		レジストリのパス名の定義
//
$REG_ROOT = "Sugi_Macro\\TeXModuleControl";	//	マクロのルートキー
$REG_K_PI_BASE = "Platform\\" + $DVIWARE + "\\DVIware";
	//	プラットフォームのサブキー
$REG_K_MACROCONFIG	=	"MacroConfig";	//	マクロの設定定義のサブキー

//	Get MacroFolder's Name
call _kernel_getmacrodir;
$MACRODIR = $$return + "\\tex_mc";

//	Message Data Information
$MSGINI	=	$MACRODIR + "\\message.ini";

//	Dynamic Data Information
$DYNINI	=	$MACRODIR + "\\dyndata.ini";
$DYN_MODULECMD	=	"ModuleCmd";	//	モジュール動作定義のサブキー
$DYN_MC_ACTION  =   "Action";
$DYN_MC_RETURN	=	"return_preview";
$DYN_PROJECT	=	"CurrentProjectInfo";	//	処理するファイルの情報

//	Current Project Information
//		コンパイル対象ファイルの情報
//
$FILENAME	=	getinistr($DYNINI,$DYN_PROJECT,"FileName");
$SHORTNAME	=	getinistr($DYNINI,$DYN_PROJECT,"ShortName");
$PROJECTDIR	=	getinistr($DYNINI,$DYN_PROJECT,"ProjectDir");
$OUTPUTEXT	=	getinistr($DYNINI,$DYN_PROJECT,"OutputExt");

call _kernel_getdatanum $REG_K_MACROCONFIG + "\\DVIware", "OpenType";
#OPENTYPE = ##return;

//----------------------------------//
//	Platform dependent codes		//
//----------------------------------//

$REG_ENUM[0]	=	"BaseDir";
$REG_ENUM[1]	=	"CmdArgv";
call _kernel_getenumdatastr $REG_K_PI_BASE, 2;
$BASEDIR		=	$REG_VALUE[0];
$CMDARGV		=	$REG_VALUE[1];


main:
	#ACTION = getininum($DYNINI, $DYN_MODULECMD, $DYN_MC_ACTION);
	if (#ACTION == 0) {
		call _local_preview;
		##ret = ##return;
	} else if (#ACTION == 1) {
//		call _local_close;
		##ret = #RETURN_SUCCESS;
	}
	writeininum $DYNINI, $DYN_MODULECMD, $DYN_MC_RETURN, ##ret;
	endmacro;


_local_preview:
	if ($OUTPUTEXT != ".dvi") {
		message "このファイルは dvi2tty では処理できません。";
		return #RETURN_ERROR;
	}
	$DVIFILE = $PROJECTDIR + "\\" + $SHORTNAME + ".dvi";
	if (!existfile($DVIFILE)) {
		$MSGARG[0] = $DVIFILE;
		call _kernel_showmsg 0, "Error_File_notExist";
		return #RETURN_ERROR;
	}
	run "\"" + $BASEDIR + "\\" + $CMDARGV + "\" \"" + $DVIFILE + "\" >con";
	if (result) ##ret = #RETURN_SUCCESS;
	else ##ret = #RETURN_ERROR;
	return ##ret;

//Endof_Main

//------------------------------------------//
//	- Module Control Service Functions -	//
//------------------------------------------//

