//--------------------------------------------------------------------------//
//		TeX Module Control with HIDEMARU Macro								//
//																			//
//			Sugi-Macro  -Macro Factory for HIDEMARU Editor-					//
//			%COPYRIGHT%		//
//																			//
//--------------------------------------------------------------------------//

//!HEADER
//	INTERNAL VERSION	%VER_MAJOR%.%VER_MINOR%.%VER_RELEASE%
//	PURPOSE				INSTALLATION MANAGEMENT
//	PLATFORM			Adobe Acrobat Reader for Windows 95/NT
//

//--------------------------------------------------------------------------//
//	このマクロは Adobe Acrobat Reader 専用です。							//
//--------------------------------------------------------------------------//

//	Platform identifier
//		呼び出しマクロが正しいかどうかのチェックやレジストリアクセスに使用
//		レジストリキー名の仕様に準拠する必要がある
//
$PNAME	=	"ADOBE_AR";

//	レジストリキーの定義
#REG_VERSION			=	%VER_MINOR%;
$REG_ROOT				=	"Sugi_Macro\\TeXModuleControl";
$REG_K_VERSION			=	"VersionInfo";
$REG_K_MACROCONFIG		=	"MacroConfig";
$REG_K_PLATFORM			=	"Platform\\" + $PNAME;
$REG_K_P_DVIWARE		=	$REG_K_PLATFORM + "\\DVIware";

//	Get MacroFolder's Name
call _kernel_getmacrodir;
$MACRODIR = $$return + "\\tex_mc";

//	文字列定義Iniファイル
$MSGINI	=	$MACRODIR + "\\message.ini";

//	補助ツールのフォルダ名及びツール起動時のコマンドライン
$TOOLDIR	=	$MACRODIR + "\\tools";
$LFNCMD		=	"\"" + $TOOLDIR + "\\getlfn.exe\" ";
$LFNINI		=	$TOOLDIR + "\\getlfn.ini";


main:
	call _kernel_getdatastr $REG_K_PLATFORM, "PlatformName";
	$MSGARG[0] = $$return;
	call _kernel_getdatanum $REG_K_PLATFORM, "InstalledWareFlag";
	#INST_SP_PTYPE = ##return;
	if (#INST_SP_PTYPE&2) call _install_exitinstall 0;

	call _local_findpathfromreg;
	$BASEDIR = $$return;
	if ($BASEDIR == "") $BASEDIR = leftstr(hidemarudir,3)+"acrobat3\\reader";
	if (!existfile($BASEDIR+"\\acrord32.exe")) {
		_input_folder:
		call _install_inputfoldername $MSGARG[0], $BASEDIR;
		if ($$return != "") $BASEDIR = $$return;
		else {
			call _kernel_showmsg 1, "Question_Inst_SkipInstall";
			if (##return) call _install_exitinstall 0;
			else goto _input_folder;
		}
	}
	call _install_getlongfilename $BASEDIR + "\\foo.txt";
	$REG_VALUE[0] = $DIRECTORY;
	$REG_VALUE[1] = "acrord32.exe";
	$REG_VALUE[2] = "\"$f\\$s.pdf\"";

	title $MSGARG[0] + " 構成情報の書き込み中...";
	$REG_ENUM[0] = "BaseDir";
	$REG_ENUM[1] = "CmdArgv";
	$REG_ENUM[2] = "CmdOpt";
	$REG_ENUM[3] = "TitleBarString";
	$REG_VALUE[3] = "Acrobat Reader - [$s.pdf]";
	call _kernel_writeenumdatastr $REG_K_P_DVIWARE, 4;
	#INST_SP_PTYPE = #INST_SP_PTYPE | 2;
	title 0;

	call _install_exitinstall 0;
	endmacro;	//	not reached.

//	End of Main

_local_findpathfromreg:
	// find ver 3
	openreg "CLASSESROOT", "Software\\Adobe\\AcrobatReader\\InstallPath";
	if (result) {
		$BASEDIR = getregstr("");
		closereg;
		call _common_removelastsep $BASEDIR;
		return $$return;
	}
	// find ver 4
	openreg "CLASSESROOT", "Software\\Adobe\\Acrobat Reader\\4.0\\InstallPath";
	if (result) {
		$BASEDIR = getregstr("");
		closereg;
		call _common_removelastsep $BASEDIR;
		return $$return;
	}
	// find ver 5
	openreg "CLASSESROOT", "Software\\Adobe\\Acrobat Reader\\5.0\\InstallPath";
	if (result) {
		$BASEDIR = getregstr("");
		closereg;
		call _common_removelastsep $BASEDIR;
		return $$return;
	}
	return "";

//------------------------------------------//
//	- Module Control Service Functions -	//
//------------------------------------------//

