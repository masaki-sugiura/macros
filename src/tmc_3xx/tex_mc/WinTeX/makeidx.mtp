//--------------------------------------------------------------------------//
//		TeX Module Control with HIDEMARU Macro								//
//																			//
//			Sugi-Macro  -Macro Factory for HIDEMARU Editor-					//
//			%COPYRIGHT%		//
//																			//
//--------------------------------------------------------------------------//

//!HEADER
//	INTERNAL VERSION	%VER_MAJOR%.%VER_MINOR%.%VER_RELEASE%
//	PURPOSE				MODULE CONTROL MICRO KERNEL
//	PLATFORM			TeX for Windows
//

//--------------------------------------------------------------------------//
//	このマクロは TeX for Windows 専用です。									//
//--------------------------------------------------------------------------//

//	Platform identifier
//		呼び出しマクロが正しいかどうかのチェックやレジストリアクセスに使用
//		レジストリキー名の仕様に準拠する必要がある
//
$MKIDXWARE	=	"WinTeX";

//----------------------------------//
//	Common global data				//
//----------------------------------//

//	Definition of return code from module control micro-kernel
//		マクロからの戻り値の定義
//
#RETURN_COMPLETE	=	  0;		//	コンパイルが正常に終了
#RETURN_ERROR		=	255;		//	コンパイル実行エラー

//	Base registry path
//		レジストリのパス名の定義
//
$REG_ROOT = "Sugi_Macro\\TeXModuleControl";	//	マクロのルートキー
$REG_K_PI_BASE = "Platform\\" + $MKIDXWARE + "\\MkIdxware";
	//	プラットフォームのサブキー
$REG_K_MACROCONFIG	=	"MacroConfig";	//	マクロの設定定義のサブキー

//	Get MacroFolder's Name
call _kernel_getmacrodir;
$MACRODIR = $$return + "\\tex_mc";

//	Message Data Information
$MSGINI	=	$MACRODIR + "\\message.ini";

//	Dynamic Data Information
$DYNINI	=	$MACRODIR + "\\dyndata.ini";
$DYN_MODULECMD	=	"ModuleCmd";	//	モジュール動作定義のサブキー
$DYN_MC_RETURN	=	"return_makeidx";	//	モジュールの戻り値
$DYN_PROJECT	=	"CurrentProjectInfo";

//	Current Project Information
//		コンパイル対象ファイルの情報
//
$FILENAME	=	getinistr($DYNINI,$DYN_PROJECT,"FileName");
$SHORTNAME	=	getinistr($DYNINI,$DYN_PROJECT,"ShortName");
$PROJECTDIR	=	getinistr($DYNINI,$DYN_PROJECT,"ProjectDir");

//----------------------------------//
//	Platform dependent codes		//
//----------------------------------//

////////	MkIdxware Information
call _kernel_getdatastr $REG_K_PI_BASE, "BaseDir";
$MKIDXWAREDIR = $$return;
$CMDARGV = "winmkidx";

//	Start Main Routine
main:
	##hwnd = hidemaruhandle( 0 );
	##hwnd_mkidx = findwindow($CMDARGV);
	if( !##hwnd_mkidx ){
		run "\"" + $MKIDXWAREDIR + "\\" + $CMDARGV + ".exe\"";
		if( !result ){
			$MSGARG[0] = $CMDARGV;
			call _kernel_showmsg 0, "Error_Module_ExeOpen";
			writeininum $DYNINI, $DYN_MODULECMD, $DYN_MC_RETURN, #RETURN_ERROR;
			endmacro;
		}
		goto main;
	}
	ddeinitiate $CMDARGV, $CMDARGV;
	if( !result ){
		##s = sendmessage(##hwnd_mkidx,0x0112,0xF060,0);
		$MSGARG[0] = $CMDARGV;
		call _kernel_showmsg 0, "Error_Module_DDEInit";
		writeininum $DYNINI, $DYN_MODULECMD, $DYN_MC_RETURN, #RETURN_ERROR;
		endmacro;
	}
	$name = "";
	ddestartadvice "Advise", $name;
	if( !result ){
		ddestopadvice "Advise";
		ddeterminate;
		##Send = sendmessage(##hwnd_mkidx,0x0112,0xF060,0);
		$MSGARG[0] = $CMDARGV;
		call _kernel_showmsg 0, "Error_Module_StartAdvice";
		continue;
	}
	ddeexecute "[winmkidx(" + $PROJECTDIR + "\\" + $SHORTNAME + ")]";
	while( $name == "" ) ddewaitadvice  "Advise", 999;
	ddestopadvice "Advise";
	ddeterminate;
	##s = sendmessage(##hwnd_mkidx,0x0112,0xF060,0);

	if( $name == "ERROR" ) ##answer = #RETURN_ERROR;
	else if( $name == "OK" ) ##answer = #RETURN_COMPLETE;
	writeininum $DYNINI, $DYN_MODULECMD, $DYN_MC_RETURN, ##answer;
	endmacro;

//	End Main Routine


//------------------------------------------//
//	- Module Control Service Functions -	//
//------------------------------------------//

